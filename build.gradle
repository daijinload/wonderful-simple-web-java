apply plugin: 'idea'
apply plugin: 'war'

ext {
    jdkLevel = 1.8
    encoding = 'UTF-8'
}

version = '0.1'

repositories {
    mavenCentral()
}

configurations {
    jetty
}

dependencies {
    jetty 'org.eclipse.jetty:jetty-runner:9.4.14.v20181114'

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.1.1'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.1.1'
}

//compileJava.options.encoding = "UTF-8"

tasks.withType(JavaCompile) {
    sourceCompatibility = jdkLevel
    targetCompatibility = jdkLevel
    options.encoding = encoding
    classpath += configurations.jetty
}

sourceSets {
    test {
        //if you truly want to override the defaults:
        output.resourcesDir = file('src/test/webapp/WEB-INF/resources')
        // Compiled Java classes should use this directory
        java.outputDir = file('src/test/webapp/WEB-INF/classes')
    }
}

javadoc {
    options.encoding = encoding
    classpath += configurations.jetty
}

test {
    classpath += configurations.jetty
    useJUnitPlatform()
    testLogging {
        exceptionFormat 'full'
        events 'started', 'skipped', 'passed', 'failed'
    }
//    reports {
//        html.enabled = true
//    }
}

idea {
    project {
        languageLevel = jdkLevel
    }
    module {
        scopes.COMPILE.plus += [configurations.jetty]
    }
}

war {
    manifest {
        attributes 'Main-Class': 'com.gmail.daijinload.Main'
    }
    from {
        configurations.jetty.collect {
            it.isDirectory()? it: zipTree(it)
        }
    } {
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
    }
    from fileTree(dir: 'build/classes/java/main', include: '**/Main.class')
}